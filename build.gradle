apply plugin: 'java'
apply plugin: 'maven'

version = '1.0'
group = 'edu.ucar.unidata'
description = "Conway's Game of Life"

def nexusUrl = 'http://artifacts.unidata.ucar.edu/content/'

configurations {
    deployerJars
}

repositories {
    mavenRepo url: nexusUrl + 'groups/unidata'
    mavenCentral()
}

dependencies {
    compile 'net.jcip:jcip-annotations:1.0+'
    testCompile 'junit:junit:4.8+'
    testCompile 'org.mockito:mockito-core:1.9.0+'
    deployerJars "org.apache.maven.wagon:wagon-http:2.2+"
}

test {
    include 'Test*'
}

task slowTests(type: Test) {
    include 'Check*'
    mustRunAfter test
}

jar {
    dependsOn configurations.runtime
    from {configurations.runtime.collect {it.isDirectory() ? it : zipTree(it)}}
    manifest {
        attributes 'Main-Class': 'Controller'
    }
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: nexusUrl + "repositories/unidata-releases") {
            authentication(userName: nexusUsername, password: nexusPassword)
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.7' 
}